plugins {
    id 'java'
}

group = 'com.gmail.necnionch'
version = '1.3.1'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

}

configurations {
    extraLibs
}


dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'

    // https://mvnrepository.com/artifact/org.openjdk.nashorn/nashorn-core
    extraLibs('org.openjdk.nashorn:nashorn-core:15.3') {
    }

    configurations.implementation.extendsFrom(configurations.extraLibs)

}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }

}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }

}


jar {
    from {
        exclude '**/module-info.class'
        configurations.extraLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}


